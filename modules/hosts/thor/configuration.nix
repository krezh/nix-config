{ inputs, ... }:
{
  flake.modules.nixos.thor =
    { pkgs, lib, ... }:
    {
      imports = with inputs.self.modules.nixos; [
        system-desktop
        efi
        desktop-utils
        secureboot
        impermanence
        amd
        openssh
        gaming
        hyprland
        containers
        wooting
        inputs.silentSDDM.nixosModules.default
      ];

      nixpkgs.overlays = [
        inputs.nix-cachyos-kernel.overlay
      ];

      networking = {
        hostName = "thor";
        networkmanager = {
          enable = true;
          wifi.backend = "iwd";
        };
        wireless.enable = lib.mkForce false;
      };

      programs = {
        silentSDDM = {
          enable = true;
          theme = "catppuccin-mocha";
          settings = { };
        };
        seahorse.enable = true;
        nix-ld.enable = true;
        appimage = {
          enable = true;
          binfmt = true;
        };
        sniffnet.enable = true;
        headsetcontrol.enable = true;
      };

      catppuccin.sddm.enable = false;

      # Display manager
      services = {
        displayManager = {
          sddm = {
            enable = true;
            wayland.enable = true;
            wayland.compositor = "weston";
            autoNumlock = true;
          };
          defaultSession = "hyprland";
        };

        # System services
        fwupd.enable = true;
        accounts-daemon.enable = true;
        gnome = {
          gnome-online-accounts.enable = true;
          gnome-keyring.enable = true;
        };
        dbus.packages = with pkgs; [
          gnome-keyring
          gcr_4
          seahorse
          libsecret
          libgnome-keyring
        ];
        flatpak = {
          enable = true;
        };

        # Misc services
        fstrim.enable = true;
        libinput = {
          enable = true;
          mouse.accelProfile = "flat";
          touchpad.accelProfile = "flat";
        };
        timesyncd.servers = [ ];
      };

      # Boot configuration
      boot = {
        plymouth.enable = false;
        kernelPackages = pkgs.linuxPackagesFor (
          pkgs.cachyosKernels.linux-cachyos-latest.override {
            cpusched = "eevdf";
            lto = "thin";
            processorOpt = "x86_64-v4";
            hzTicks = "1000";
            bbr3 = true;
          }
        );
        tmp.cleanOnBoot = true;
        kernel.sysctl = {
          "kernel.core_pattern" = "|/bin/false";
          "kernel.core_uses_pid" = 0;
        };
      };

      # Disable coredump
      systemd = {
        coredump.enable = false;
        oomd.enableUserSlices = true;
      };

      security = {
        pam = {
          loginLimits = [
            {
              domain = "*";
              type = "hard";
              item = "core";
              value = "0";
            }
            {
              domain = "*";
              type = "soft";
              item = "core";
              value = "0";
            }
          ];
          # GNOME keyring
          services = {
            sddm.enableGnomeKeyring = true;
            hyprlock.enableGnomeKeyring = true;
            login.enableGnomeKeyring = true;
          };
        };
      };

      environment.systemPackages = with pkgs; [
        age-plugin-yubikey
        age-plugin-fido2-hmac
        nautilus
        libnotify
        pwvucontrol
        alsa-utils
        pavucontrol
        inputs.comin.packages.${stdenv.hostPlatform.system}.default
      ];
    };
}
